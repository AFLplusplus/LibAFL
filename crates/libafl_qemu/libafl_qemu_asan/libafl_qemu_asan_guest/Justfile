import "../../../../just/libafl-qemu.just"
import "../../../libafl_asan/libafl_asan_libc/Justfile"
import "../../../libafl_asan/fuzzer_name.just"

GUEST_SOURCE_DIR := source_directory()

[unix]
compile_guest:
	#!/bin/sh
	. {{ DOTENV }}
	cargo \
		build \
		--package libafl_qemu_asan_guest \
		--target ${CROSS_TARGET} \
		--profile {{ PROFILE }} \
		--target-dir {{ TARGET_DIR }}

[unix]
fix_guest:
	#!/bin/sh
	. {{ DOTENV }}
	cargo \
		fix \
		--package libafl_qemu_asan_guest \
		--target ${CROSS_TARGET} \
		--profile {{ PROFILE }} \
		--target-dir {{ TARGET_DIR }} \
		--features test \
		--allow-dirty

[unix]
rel_guest: compile_guest build_libc
	#!/bin/sh
	. {{ DOTENV }}
	${CROSS_CC} \
		${LIBAFL_QEMU_ASAN_CFLAGS} \
		-r \
		-nodefaultlibs \
		-nostartfiles \
		-nostdlib \
		-g \
		-u patch_aligned_alloc \
		-u patch_atoi \
		-u patch_atol \
		-u patch_atoll \
		-u patch_calloc \
		-u patch_free \
		-u patch_malloc \
		-u patch_malloc_usable_size \
		-u patch_memalign \
		-u patch_memset \
		-u patch_mmap \
		-u patch_munmap \
		-u patch_posix_memalign \
		-u patch_pvalloc \
		-u patch_read \
		-u patch_realloc \
		-u patch_reallocarray \
		-u patch_valloc \
		-u patch_write \
		-o {{ TARGET_DIR }}/$CROSS_TARGET/{{ PROFILE_DIR }}/libafl_qemu_asan_guest.rel \
		{{ TARGET_DIR }}/$CROSS_TARGET/{{ PROFILE_DIR }}/libafl_qemu_asan_guest.a \

[unix]
rename_guest: rel_guest
	#!/bin/sh
	. {{ DOTENV }}
	${CROSS_OBJCOPY} \
		--redefine-syms={{ GUEST_SOURCE_DIR }}/rename_real.syms \
		--redefine-syms={{ GUEST_SOURCE_DIR }}/rename_patch.syms \
		{{ TARGET_DIR }}/$CROSS_TARGET/{{ PROFILE_DIR }}/libafl_qemu_asan_guest.rel \
		{{ TARGET_DIR }}/$CROSS_TARGET/{{ PROFILE_DIR }}/libafl_qemu_asan_guest.renamed

[unix]
link_guest: rename_guest
	#!/bin/sh
	. {{ DOTENV }}
	${CROSS_CC} \
		-shared \
		-nodefaultlibs \
		-nostartfiles \
		-nostdlib \
		-g \
		-u aligned_alloc \
		-u atoi \
		-u atol \
		-u atoll \
		-u calloc \
		-u free \
		-u malloc \
		-u malloc_usable_size \
		-u memalign \
		-u memset \
		-u mmap \
		-u munmap \
		-u posix_memalign \
		-u pvalloc \
		-u read \
		-u realloc \
		-u reallocarray \
		-u valloc \
		-u write \
		-Wl,--version-script={{ GUEST_SOURCE_DIR }}/guest.map \
		-Wl,--gc-sections \
		-Wl,--no-undefined \
		-o {{ TARGET_DIR }}/$CROSS_TARGET/{{ PROFILE_DIR }}/_libafl_qemu_asan_guest.so \
		{{ TARGET_DIR }}/$CROSS_TARGET/{{ PROFILE_DIR }}/libafl_qemu_asan_guest.renamed \
		-L {{ TARGET_DIR }}/$CROSS_TARGET/{{ PROFILE_DIR }} \
		"-lafl_asan_libc"

strip_guest: link_guest
	#!/bin/sh
	. {{ DOTENV }}
	${CROSS_STRIP} \
		--strip-unneeded \
		-o {{ TARGET_DIR }}/$CROSS_TARGET/{{ PROFILE_DIR }}/libafl_qemu_asan_guest.so \
		{{ TARGET_DIR }}/$CROSS_TARGET/{{ PROFILE_DIR }}/_libafl_qemu_asan_guest.so

[unix]
build_guest: strip_guest
