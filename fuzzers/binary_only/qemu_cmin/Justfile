import "../../../just/libafl-qemu-libpng.just"

FUZZER_NAME := "qemu_cmin"
HARNESS := TARGET_DIR / ("libpng-harness-" + PROFILE)

[unix]
build arch=DEFAULT_ARCH:
    cargo build \
      --profile {{ PROFILE }} \
      --features {{ arch }} \
      --target-dir {{ TARGET_DIR }}

[unix]
harness arch=DEFAULT_ARCH: (libpng arch)
    {{ CROSS_CXX }} \
        ./harness.cc \
        {{ CROSS_CFLAGS }} \
        "{{TARGET_DIR}}/build-png/.libs/libpng16.a" \
        "{{TARGET_DIR}}/build-zlib/libz.a" \
        -I"{{TARGET_DIR}}/build-png" \
        -I"{{TARGET_DIR}}/build-zlib/zlib/lib" \
        -L"{{TARGET_DIR}}/build-zlib/zlib/lib" \
        -o"{{ HARNESS }}" \
        -lm -static

[unix]
run arch=DEFAULT_ARCH: (harness arch) (build arch)
    {{ FUZZER }} \
        --output ./output \
        --input ./corpus \
        --verbose \
        -- {{ HARNESS }}

[unix]
test: (run "x86_64") (run "arm")

[unix]
clean:
    cargo clean