import "../../../just/libafl.just"
FUZZER_NAME := "frida_libpng"

set windows-shell = ["cmd.exe", "/c"]

[linux]
[macos]
libpng:
    #!/bin/bash
    if [ ! -f v1.6.37.tar.gz ]; then
        wget https://github.com/glennrp/libpng/archive/refs/tags/v1.6.37.tar.gz
    fi
    tar -xvf v1.6.37.tar.gz

[linux]
[macos]
lib: libpng
    #!/bin/bash
    cd libpng-1.6.37 && ./configure --enable-shared=no --with-pic=yes --enable-hardware-optimizations=yes
    cd {{PROJECT_DIR}}
    make -C libpng-1.6.37 CC="{{LIBAFL_CC}}" CXX="{{LIBAFL_CXX}}"


[linux]
[macos]
harness: lib
    #!/bin/bash
    clang++ -O3 -c -fPIC harness.cc -o harness.o
    clang++ -O3 harness.o libpng-1.6.37/.libs/libpng16.a -shared -lz -o libpng-harness.so

[windows]
harness:
    cl /c harness_win.cpp && link harness_win.obj /dll

[linux]
[macos]
fuzzer:
    #!/bin/bash
    cargo build --profile {{PROFILE}}
    cp {{TARGET_DIR}}/{{PROFILE_DIR}}/{{FUZZER_NAME}} .

[windows]
    cargo build --profile ${PROFILE}
    cp .\target\{{PROFILE_DIR}}\{{FUZZER_NAME}} .

[linux]
[macos]
run: fuzzer harness
    ./{{FUZZER_NAME}} -F LLVMFuzzerTestOneInput -H ./libpng-harness.so -l ./libpng-harness.so

[windows]
run: fuzzer harness
    .\{{FUZZER_NAME}} -F LLVMFuzzerTestOneInput -H .\harness_win.dll -l .\harness_win.dll --cores=0

[linux]
[macos]
test: fuzzer harness
    #!/bin/bash
    rm -rf libafl_unix_shmem_server || true
    timeout 30s ./{{FUZZER_NAME}} -F LLVMFuzzerTestOneInput -H ./libpng-harness.so -l ./libpng-harness.so 2>/dev/null  | tee fuzz_stdout.log || true
    if grep -qa "corpus: 70" fuzz_stdout.log; then
        echo "Fuzzer is working"
    else
        echo "Fuzzer does not generate any testcases or any crashes"
        exit 1
    fi

[windows]
test: fuzzer harness
    start "frida_fuzzer.exe" -F LLVMFuzzerTestOneInput -H .\harness_win.dll -l .\harness_win.dll --cores=0
    #ping is for timeout
    ping -n 10 127.0.0.1>NUL && taskkill /im frida_fuzzer.exe /F
    dir /a-d "corpus_discovered\*" && (echo Files exist) || (exit /b 1337)

[linux]
[macos]
clean:
    rm -rf {{FUZZER_NAME}}
    make -C libpng-1.6.37 clean
    cargo clean