import "../../../just/libafl.just"

FUZZER_NAME := "frida_executable_fuzzer"

[unix]
[macos]
harness:
    cc -c libfuzzer_main.c
    cc \
        ./fuzz.c \
        ./libfuzzer_main.o \
        -o {{ FUZZER_NAME }} \
        -lm -lz

[unix]
[macos]
build:
    cargo build --profile {{ PROFILE }}

[unix]
[macos]
run: build harness
    cargo run \
      --profile {{ PROFILE }} \
      ./{{ FUZZER_NAME }} \
      -- \
      --libafl-in ./corpus \
      --libafl-out ./out \
      ./{{ FUZZER_NAME }}


[unix]
test: build harness
    #!/bin/bash

    timeout 15s {{ FUZZER }} ./harness -- --libafl-in ../../inprocess/libfuzzer_libpng/corpus --libafl-out out ./harness | tee fuzz_stdout.log
    if grep -qa "corpus: 5" fuzz_stdout.log; then
        echo "Fuzzer is working"
    else
        echo "Fuzzer does not generate any testcases or any crashes"
        exit 1
    fi

[unix]
clean:
    rm -rf ./libpng-harness
    make -C libpng-1.6.37 clean
    cargo clean