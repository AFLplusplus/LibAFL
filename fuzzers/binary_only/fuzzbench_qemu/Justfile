import "../../../just/libafl.just"

FUZZER_NAME := "fuzzbench_qemu"

[unix]
[macos]
harness:
    cc -c libfuzzer_main.c
    cc \
        ./fuzz.c \
        ./libfuzzer_main.o \
        -o {{ FUZZER_NAME }} \
        -lm -lz

[unix]
[macos]
build:
    cargo build --profile {{ PROFILE }}

[unix]
[macos]
run: build harness
    {{ FUZZER }} \
      --libafl-in ./corpus \
      --libafl-out ./out \
      ./{{ FUZZER_NAME }} \
      -- \
      ./{{ FUZZER_NAME }}


[unix]
test: build harness
    #!/bin/bash

    timeout 15s {{ FUZZER }} ./harness -- --libafl-in ../../inprocess/libfuzzer_libpng/corpus --libafl-out out ./harness | tee fuzz_stdout.log
    if grep -qa "corpus: 5" fuzz_stdout.log; then
        echo "Fuzzer is working"
    else
        echo "Fuzzer does not generate any testcases or any crashes"
        exit 1
    fi

[unix]
clean:
    cargo clean