import "../../../just/libafl.just"

FUZZER_NAME := "fuzzbench_qemu"
HARNESS_NAME := "harness"

[unix]
harness:
    cc -c libfuzzer_main.c
    cc \
        ./fuzz.c \
        ./libfuzzer_main.o \
        -o {{ HARNESS_NAME }} \
        -lm -lz

[unix]
tests:
    cc -c {{PROJECT_DIR}}/libfuzzer_main.c
    cc -O0 ./tests/test1.c ./libfuzzer_main.o -o test1 -lm -lz
    cc -O0 ./tests/test2.c ./libfuzzer_main.o -o test2 -lm -lz
    cc -O0 ./tests/test3.c ./libfuzzer_main.o -o test3 -lm -lz


[unix]
build:
    cargo build --profile {{ PROFILE }}

[unix]
run: build harness
    {{ FUZZER }} \
      --libafl-in ./corpus \
      --libafl-out ./out \
      ./{{ HARNESS_NAME }} \
      -- \
      ./{{ HARNESS_NAME }}


[unix]
test2:
    cargo run --profile {{PROFILE}} ./test2 -- --libafl-in ../../inprocess/libfuzzer_libpng/corpus --libafl-out ./out ./test2

[unix]
test1:
    cargo run --profile {{PROFILE}} ./test1 -- --libafl-in ../../inprocess/libfuzzer_libpng/corpus --libafl-out ./out ./test1


[unix]
test: build harness
    #!/bin/bash
    timeout 15s {{ FUZZER }} ./harness -- --libafl-in ../../inprocess/libfuzzer_libpng/corpus --libafl-out out ./harness | tee fuzz_stdout.log
    if grep -qa "objectives: 5" fuzz_stdout.log; then
        echo "Fuzzer is working"
    else
        echo "Fuzzer does not generate any testcases or any crashes"
        exit 1
    fi

[unix]
clean:
    cargo clean
