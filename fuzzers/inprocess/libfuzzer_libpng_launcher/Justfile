FUZZER_NAME := 'fuzzer_libpng_launcher'
PROJECT_DIR := absolute_path(".")
PROFILE := env("PROFILE", "release")
PROFILE_DIR := if PROFILE == "release" { "release" } else if PROFILE == "dev" { "debug" } else { "debug" }
CARGO_TARGET_DIR := env("CARGO_TARGET_DIR", "target")
FUZZER := PROJECT_DIR / CARGO_TARGET_DIR / PROFILE_DIR / FUZZER_NAME
LIBAFL_CC := PROJECT_DIR / CARGO_TARGET_DIR / PROFILE_DIR / "libafl_cc"
LIBAFL_CXX := PROJECT_DIR / CARGO_TARGET_DIR / PROFILE_DIR / "libafl_cxx"
LIBTOOL := PROJECT_DIR / CARGO_TARGET_DIR / PROFILE_DIR / "libafl_libtool"


alias cc := cxx

[linux]
[macos]
libpng:
    #!/bin/bash
    if [ ! -f v1.6.37.tar.gz ]; then
        wget https://github.com/glennrp/libpng/archive/refs/tags/v1.6.37.tar.gz
    fi
    tar -xvf v1.6.37.tar.gz

[windows]
libpng:
    echo "Unsupported on this platform"

[linux]
[macos]
cxx:
    cargo build --profile {{PROFILE}}

[windows]
cxx:
    echo "Unsupported on this platform"

[linux]
[macos]
lib: libpng cxx
    #!/bin/bash
    cd libpng-1.6.37 && CC={{LIBAFL_CC}} CXX={{LIBAFL_CXX}} ./configure --enable-shared=no --with-pic=yes --enable-hardware-optimizations=no
    cd {{PROJECT_DIR}}
    # Patch for macOS: fp.h is missing in newer SDKs, use math.h instead
    sed -i.bak 's|include <fp.h>|include <math.h>|g' libpng-1.6.37/pngpriv.h
    make -C libpng-1.6.37 CC="{{LIBAFL_CC}}" CXX="{{LIBAFL_CXX}}" LIBTOOL="{{LIBTOOL}}" libpng16.la

[windows]
lib:
    echo "Unsupported on this platform"

[linux]
[macos]
fuzzer: lib cxx
    {{LIBAFL_CXX}} {{PROJECT_DIR}}/harness.cc {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.a -I {{PROJECT_DIR}}/libpng-1.6.37/ -o {{FUZZER_NAME}} -lm -lz

[windows]
fuzzer:
    echo "Unsupported on this platform"

[linux]
[macos]
run: fuzzer
    ./{{FUZZER_NAME}}.coverage --broker-port 21337 --cores 0 --input ./corpus

[windows]
run: fuzzer
    echo "Unsupported on this platform"

[linux]
[macos]
test: fuzzer
    #!/bin/bash
    rm -rf libafl_unix_shmem_server || true
    echo "Testing default (no fork)..."
    timeout 31s ./{{FUZZER_NAME}}.coverage --broker-port 21337 --cores 0 --input ./corpus  2>&1 | tee fuzz_stdout.log || true
    if grep -qa "corpus: " fuzz_stdout.log; then
        echo "Fuzzer (no fork) is working"
    else
        echo "Fuzzer (no fork) does not generate any testcases or any crashes"
        # exit 1
    fi

    echo "Testing with fork..."
    rm -rf libafl_unix_shmem_server || true
    rm -rf out_fork || true
    timeout 31s ./{{FUZZER_NAME}}.coverage --broker-port 21338 --cores 0 --input ./corpus --output ./out_fork --fork 2>&1 | tee fuzz_fork_stdout.log || true
    if grep -qa "corpus: " fuzz_fork_stdout.log; then
        echo "Fuzzer (fork) is working"
    else
        echo "Fuzzer (fork) does not generate any testcases or any crashes"
        # exit 1
    fi

    echo "Testing with fork and crash..."
    rm -rf libafl_unix_shmem_server || true
    rm -rf out_fork_crash || true
    # Run with crash_after. It should crash and restart repeatedly.
    # We check if it still produces output/runs.
    timeout 31s ./{{FUZZER_NAME}}.coverage --broker-port 21339 --cores 0 --input ./corpus --output ./out_fork_crash --fork --crash-after 100 2>&1 | tee fuzz_fork_crash_stdout.log || true
    # Check for objectives (crashes are recorded as objectives in the stats)
    # Note: forked child's stderr may not propagate, so we check stats output
    if grep -qa "objectives: [1-9]" fuzz_fork_crash_stdout.log; then
        echo "Fuzzer (fork+crash) recorded crashes as objectives"
    else
        echo "Fuzzer (fork+crash) did not record any objectives"
        exit 1
    fi
    # Check if it kept working (found entries)
    if grep -qa "corpus: " fuzz_fork_crash_stdout.log; then
       echo "Fuzzer (fork+crash) is working (finding corpus)"
    else
       echo "Fuzzer (fork+crash) failed to find corpus"
       exit 1 
    fi

    # Verify respawn by checking that objectives count continues to grow
    # (each crash at 100 execs = 1 objective, so at 500+ execs we should have 5+ objectives)
    LAST_OBJ=$(grep -oE 'objectives: [0-9]+' fuzz_fork_crash_stdout.log | tail -1 | grep -oE '[0-9]+')
    if [ "$LAST_OBJ" -gt "3" ]; then
       echo "Fuzzer (fork+crash) respawned multiple times: $LAST_OBJ objectives found"
    else
       echo "Fuzzer (fork+crash) did not respawn enough (only $LAST_OBJ objectives)"
       exit 1
    fi

    # Verify progress continues after crashes (corpus increases past initial crash point)
    # Extract last corpus value and ensure it grew past initial crash point
    LAST_CORPUS=$(grep -oE 'corpus: [0-9]+' fuzz_fork_crash_stdout.log | tail -1 | grep -oE '[0-9]+')
    if [ "$LAST_CORPUS" -gt "20" ]; then
       echo "Fuzzer (fork+crash) continues progress: last corpus = $LAST_CORPUS"
    else
       echo "Fuzzer (fork+crash) did not find enough corpus entries (only $LAST_CORPUS)"
       exit 1
    fi

[windows]
test: fuzzer
    echo "Unsupported on this platform"

clean:
    rm -rf {{FUZZER_NAME}}
    make -C libpng-1.6.37 clean || true
    cargo clean
