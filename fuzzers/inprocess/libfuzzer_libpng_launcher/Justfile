FUZZER_NAME := 'fuzzer_libpng_launcher'
PROJECT_DIR := absolute_path(".")
PROFILE := env("PROFILE", "release")
PROFILE_DIR := if PROFILE == "release" { "release" } else if PROFILE == "dev" { "debug" } else { "debug" }
CARGO_TARGET_DIR := env("CARGO_TARGET_DIR", "target")
FUZZER := PROJECT_DIR / CARGO_TARGET_DIR / PROFILE_DIR / FUZZER_NAME
LIBAFL_CC := PROJECT_DIR / CARGO_TARGET_DIR / PROFILE_DIR / "libafl_cc"
LIBAFL_CXX := PROJECT_DIR / CARGO_TARGET_DIR / PROFILE_DIR / "libafl_cxx"
LIBTOOL := PROJECT_DIR / CARGO_TARGET_DIR / PROFILE_DIR / "libafl_libtool"


alias cc := cxx

[linux]
[macos]
libpng:
    #!/bin/bash
    if [ ! -f v1.6.37.tar.gz ]; then
        wget https://github.com/glennrp/libpng/archive/refs/tags/v1.6.37.tar.gz
    fi
    tar -xvf v1.6.37.tar.gz

[windows]
libpng:
    echo "Unsupported on this platform"

[linux]
[macos]
cxx:
    cargo build --profile {{PROFILE}}

[windows]
cxx:
    echo "Unsupported on this platform"

[linux]
[macos]
lib: libpng cxx
    #!/bin/bash
    if [ ! -f libpng-1.6.37/.libs/libpng16.a ]; then
        cd libpng-1.6.37 && CC={{LIBAFL_CC}} CXX={{LIBAFL_CXX}} ./configure --enable-shared=no --with-pic=yes --enable-hardware-optimizations=no
    else
        echo "libpng already built, skipping configure..."
    fi
    cd {{PROJECT_DIR}}
    # Patch for macOS: fp.h is missing in newer SDKs, use math.h instead
    sed -i.bak 's|include <fp.h>|include <math.h>|g' libpng-1.6.37/pngpriv.h
    make -C libpng-1.6.37 libpng16.la CC="{{LIBAFL_CC}}" CXX="{{LIBAFL_CXX}}" LIBTOOL="{{LIBTOOL}}"

[windows]
lib:
    echo "Unsupported on this platform"

[linux]
fuzzer: lib cxx
    {{LIBAFL_CXX}} {{PROJECT_DIR}}/harness.cc {{PROJECT_DIR}}/target/{{PROFILE_DIR}}/liblibfuzzer_libpng.a {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.coverage.a -I {{PROJECT_DIR}}/libpng-1.6.37/ -o {{FUZZER_NAME}} -lm -lz --libafl-ignore-configurations -Wl,--allow-multiple-definition

[macos]
fuzzer: lib cxx
    {{LIBAFL_CXX}} {{PROJECT_DIR}}/harness.cc {{PROJECT_DIR}}/target/{{PROFILE_DIR}}/liblibfuzzer_libpng.a {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.coverage.a -I {{PROJECT_DIR}}/libpng-1.6.37/ -o {{FUZZER_NAME}} -lm -lz -framework CoreFoundation -framework Security

[windows]
fuzzer:
    echo "Unsupported on this platform"

[linux]
[macos]
run: fuzzer
    ./{{FUZZER_NAME}}.coverage --broker-port 21337 --cores 0 --input ./corpus

[windows]
run: fuzzer
    echo "Unsupported on this platform"

[linux]
[macos]
test: fuzzer test-statsd test-tcp-fork test-tcp-no-fork test-tui test-all
    #!/bin/bash
    rm -rf libafl_unix_shmem_server || true
    echo "Testing default (no fork)..."
    timeout 31s ./{{FUZZER_NAME}} --broker-port 21337 --cores 0 --input ./corpus  2>&1 | tee fuzz_stdout.log || true
    if grep -qa "corpus: " fuzz_stdout.log; then
        echo "Fuzzer (no fork) is working"
    else
        echo "Fuzzer (no fork) does not generate any testcases or any crashes"
        # exit 1
    fi

    echo "Testing with fork..."
    rm -rf libafl_unix_shmem_server || true
    rm -rf out_fork || true
    timeout 31s ./{{FUZZER_NAME}} --broker-port 21338 --cores 0 --input ./corpus --output ./out_fork --fork 2>&1 | tee fuzz_fork_stdout.log || true
    if grep -qa "corpus: " fuzz_fork_stdout.log; then
        echo "Fuzzer (fork) is working"
    else
        echo "Fuzzer (fork) does not generate any testcases or any crashes"
        # exit 1
    fi

    echo "Testing with fork and crash..."
    rm -rf libafl_unix_shmem_server || true
    rm -rf out_fork_crash || true
    # Run with crash_after. It should crash and restart repeatedly.
    # We check if it still produces output/runs.
    timeout 31s ./{{FUZZER_NAME}} --broker-port 21339 --cores 0 --input ./corpus --output ./out_fork_crash --fork --crash-after 100 2>&1 | tee fuzz_fork_crash_stdout.log || true
    # Check for objectives (crashes are recorded as objectives in the stats)
    # Note: forked child's stderr may not propagate, so we check stats output
    if grep -qa "objectives: [1-9]" fuzz_fork_crash_stdout.log; then
        echo "Fuzzer (fork+crash) recorded crashes as objectives"
    else
        echo "Fuzzer (fork+crash) did not record any objectives"
        exit 1
    fi
    # Check if it kept working (found entries)
    if grep -qa "corpus: " fuzz_fork_crash_stdout.log; then
       echo "Fuzzer (fork+crash) is working (finding corpus)"
    else
       echo "Fuzzer (fork+crash) failed to find corpus"
       exit 1 
    fi

    # Verify respawn by checking that objectives count continues to grow
    # (each crash at 100 execs = 1 objective, so at 500+ execs we should have 5+ objectives)
    LAST_OBJ=$(grep -oE 'objectives: [0-9]+' fuzz_fork_crash_stdout.log | tail -1 | grep -oE '[0-9]+')
    if [ "$LAST_OBJ" -gt "3" ]; then
       echo "Fuzzer (fork+crash) respawned multiple times: $LAST_OBJ objectives found"
    else
       echo "Fuzzer (fork+crash) did not respawn enough (only $LAST_OBJ objectives)"
       exit 1
    fi

    # Verify progress continues after crashes (corpus increases past initial crash point)
    # Extract last corpus value and ensure it grew past initial crash point
    LAST_CORPUS=$(grep -oE 'corpus: [0-9]+' fuzz_fork_crash_stdout.log | tail -1 | grep -oE '[0-9]+')
    if [ "$LAST_CORPUS" -gt "20" ]; then
       echo "Fuzzer (fork+crash) continues progress: last corpus = $LAST_CORPUS"
    else
       echo "Fuzzer (fork+crash) did not find enough corpus entries (only $LAST_CORPUS)"
       exit 1
    fi

[windows]
test: fuzzer
    echo "Unsupported on this platform"

[linux]
[macos]
cxx-statsd:
    cargo build --profile {{PROFILE}} --features statsd

[linux]
fuzzer-statsd: lib cxx-statsd
    {{LIBAFL_CXX}} {{PROJECT_DIR}}/harness.cc {{PROJECT_DIR}}/target/{{PROFILE_DIR}}/liblibfuzzer_libpng.a {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.coverage.a -I {{PROJECT_DIR}}/libpng-1.6.37/ -o {{FUZZER_NAME}}-statsd -lm -lz --libafl-ignore-configurations -Wl,--allow-multiple-definition

[macos]
fuzzer-statsd: lib cxx-statsd
    {{LIBAFL_CXX}} {{PROJECT_DIR}}/harness.cc {{PROJECT_DIR}}/target/{{PROFILE_DIR}}/liblibfuzzer_libpng.a {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.coverage.a -I {{PROJECT_DIR}}/libpng-1.6.37/ -o {{FUZZER_NAME}}-statsd -lm -lz -framework CoreFoundation -framework Security --libafl-ignore-configurations -Wl,--allow-multiple-definition

[linux]
[macos]
cxx-tcp:
    cargo build --profile {{PROFILE}} --features tcp_manager

[linux]
fuzzer-tcp: lib cxx-tcp
    cp {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.coverage_ubsan.a {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.a
    {{LIBAFL_CXX}} {{PROJECT_DIR}}/harness.cc -L {{PROJECT_DIR}}/target/{{PROFILE_DIR}} -llibfuzzer_libpng -Wl,--whole-archive {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.a -Wl,--no-whole-archive -I {{PROJECT_DIR}}/libpng-1.6.37/ -o {{FUZZER_NAME}}-tcp -lm -lz --libafl-ignore-configurations -Wl,--allow-multiple-definition -fsanitize=undefined

[macos]
fuzzer-tcp: lib cxx-tcp
    cp {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.coverage_ubsan.a {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.a
    {{LIBAFL_CXX}} {{PROJECT_DIR}}/harness.cc -L {{PROJECT_DIR}}/target/{{PROFILE_DIR}} -llibfuzzer_libpng -Wl,-force_load,{{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.a -I {{PROJECT_DIR}}/libpng-1.6.37/ -o {{FUZZER_NAME}}-tcp -lm -lz -framework CoreFoundation -framework Security --libafl-ignore-configurations -Wl,--allow-multiple-definition -fsanitize=undefined

[linux]
[macos]
test-statsd: fuzzer-statsd
    #!/bin/bash
    rm -rf libafl_unix_shmem_server || true
    rm -f statsd_out.log
    # Start python listener in background
    python3 -c "import socket; s=socket.socket(socket.AF_INET, socket.SOCK_DGRAM); s.bind(('127.0.0.1', 8126)); s.settimeout(25); d=s.recv(1024); print(d)" > statsd_out.log &
    LISTENER_PID=$!
    # Run fuzzer for a few seconds
    timeout 20s ./{{FUZZER_NAME}}-statsd --broker-port 21338 --cores 0 --input ./corpus --statsd --statsd-port 8126 > fuzzer_statsd.log 2>&1 || true
    # Wait for listener to potentially finish if it got data
    wait $LISTENER_PID || true
    if [ -s statsd_out.log ]; then
        echo "StatsD received data"
    else
        echo "StatsD failed to receive data"
        exit 1
    fi


[linux]
[macos]
test-tcp-fork: fuzzer-tcp
    #!/bin/bash
    rm -rf libafl_unix_shmem_server || true
    echo "Testing with TCP (fork)..."
    rm -rf out_tcp_fork || true
    timeout 31s ./{{FUZZER_NAME}}-tcp --broker-port 21340 --cores 0 --input ./corpus --output ./out_tcp_fork --tcp --fork > fuzz_tcp_fork.log 2>&1 || true
    if grep -qa "corpus: " fuzz_tcp_fork.log; then
        echo "Fuzzer (TCP fork) is working"
    else
        echo "Fuzzer (TCP fork) failed: no corpus found in log"
        exit 1
    fi

[linux]
[macos]
test-tcp-no-fork: fuzzer-tcp
    #!/bin/bash
    rm -rf libafl_unix_shmem_server || true
    echo "Testing with TCP (no fork)..."
    rm -rf out_tcp_no_fork || true
    timeout 31s ./{{FUZZER_NAME}}-tcp --broker-port 21341 --cores 0 --input ./corpus --output ./out_tcp_no_fork --tcp > fuzz_tcp_no_fork.log 2>&1 || true
    if grep -qa "corpus: " fuzz_tcp_no_fork.log; then
        echo "Fuzzer (TCP no fork) is working"
    else
        echo "Fuzzer (TCP no fork) failed: no corpus found in log"
        exit 1
    fi

[linux]
[macos]
cxx-tui:
    cargo build --profile {{PROFILE}} --features tui

[linux]
[macos]
fuzzer-tui: lib cxx-tui
    cp {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.coverage_ubsan.a {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.a
    {{LIBAFL_CXX}} {{PROJECT_DIR}}/harness.cc -L {{PROJECT_DIR}}/target/{{PROFILE_DIR}} -llibfuzzer_libpng -Wl,--whole-archive {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.a -Wl,--no-whole-archive -I {{PROJECT_DIR}}/libpng-1.6.37/ -o {{FUZZER_NAME}}-tui -lm -lz --libafl-ignore-configurations -Wl,--allow-multiple-definition -fsanitize=undefined

[linux]
[macos]
test-tui: fuzzer-tui
    #!/bin/bash
    rm -rf libafl_unix_shmem_server || true
    # Run fuzzer with TUI for a few seconds
    # verify it doesn't crash immediately
    timeout 5s ./{{FUZZER_NAME}}-tui --broker-port 21339 --cores 0 --input ./corpus --tui > fuzzer_tui.log 2>&1 || true
    if grep -q "panic" fuzzer_tui.log; then
        echo "Fuzzer panicked"
        cat fuzzer_tui.log
        exit 1
    fi
    echo "TUI Fuzzer ran successfully (for 5s)"


[linux]
[macos]
cxx-all:
    cargo build --profile {{PROFILE}} --features tui,tcp_manager,statsd

[linux]
[macos]
fuzzer-all: lib cxx-all
    cp {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.coverage_ubsan.a {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.a
    {{LIBAFL_CXX}} {{PROJECT_DIR}}/harness.cc -L {{PROJECT_DIR}}/target/{{PROFILE_DIR}} -llibfuzzer_libpng -Wl,--whole-archive {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.a -Wl,--no-whole-archive -I {{PROJECT_DIR}}/libpng-1.6.37/ -o {{FUZZER_NAME}}-all -lm -lz --libafl-ignore-configurations -Wl,--allow-multiple-definition -fsanitize=undefined

[linux]
[macos]
test-all: fuzzer-all
    #!/bin/bash
    rm -rf libafl_unix_shmem_server || true
    echo "Testing all features (TCP mode)..."
    rm -rf out_all_tcp || true
    timeout 20s ./{{FUZZER_NAME}}-all --broker-port 21350 --cores 0 --input ./corpus --output ./out_all_tcp --tcp > fuzzer_all_tcp.log 2>&1 || true
    if grep -qa "corpus: " fuzzer_all_tcp.log; then
        echo "Fuzzer (All+TCP) is working"
    else
        echo "Fuzzer (All+TCP) failed"
        exit 1
    fi
    echo "Testing all features (TUI mode)..."
    timeout 5s ./{{FUZZER_NAME}}-all --broker-port 21351 --cores 0 --input ./corpus --tui > fuzzer_all_tui.log 2>&1 || true
    if grep -q "panic" fuzzer_all_tui.log; then
        echo "Fuzzer (All+TUI) panicked"
        cat fuzzer_all_tui.log
        exit 1
    fi
    echo "Fuzzer (All+TUI) ran successfully"


clean:
    rm -rf {{FUZZER_NAME}}
    make -C libpng-1.6.37 clean || true
    cargo clean
