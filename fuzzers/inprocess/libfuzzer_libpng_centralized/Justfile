FUZZER_NAME := 'fuzzer_libpng_centralized'
PROJECT_DIR := absolute_path(".")
PROFILE := env("PROFILE", "release")
PROFILE_DIR := if PROFILE == "release" { "release" } else if PROFILE == "dev" { "debug" } else { "debug" }
CARGO_TARGET_DIR := env("CARGO_TARGET_DIR", "target")
FUZZER := CARGO_TARGET_DIR / PROFILE_DIR / FUZZER_NAME
LIBAFL_CC := CARGO_TARGET_DIR / PROFILE_DIR / "libafl_cc"
LIBAFL_CXX := CARGO_TARGET_DIR / PROFILE_DIR / "libafl_cxx"
LIBTOOL := CARGO_TARGET_DIR / PROFILE_DIR / "libafl_libtool"


alias cc := cxx

[linux]
[macos]
libpng:
    #!/bin/bash
    if [ ! -f v1.6.37.tar.gz ]; then
        wget https://github.com/glennrp/libpng/archive/refs/tags/v1.6.37.tar.gz
    fi
    tar -xvf v1.6.37.tar.gz

[windows]
libpng:
    echo "Unsupported on this platform"

[linux]
[macos]
cxx:
    cargo build --profile {{PROFILE}}

[windows]
cxx:
    echo "Unsupported on this platform"

[linux]
[macos]
lib: libpng cxx
    #!/bin/bash
    cd libpng-1.6.37 && ./configure --enable-shared=no --with-pic=yes --enable-hardware-optimizations=yes CC="{{LIBAFL_CC}}" CXX="{{LIBAFL_CXX}}"
    cd {{PROJECT_DIR}}
    # Patch for macOS: fp.h is missing in newer SDKs, use math.h instead
    sed -i.bak 's|include <fp.h>|include <math.h>|g' libpng-1.6.37/pngpriv.h
    make -C libpng-1.6.37 CC="{{LIBAFL_CC}}" CXX="{{LIBAFL_CXX}}" LIBTOOL="{{LIBTOOL}}" || true

[windows]
lib:
    echo "Unsupported on this platform"

[linux]
fuzzer: lib cxx
    cp {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.coverage_ubsan.a {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.a
    {{LIBAFL_CXX}} {{PROJECT_DIR}}/harness.cc -Wl,--whole-archive {{PROJECT_DIR}}/target/{{PROFILE_DIR}}/liblibfuzzer_libpng.a -Wl,--no-whole-archive -Wl,--whole-archive {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.a -Wl,--no-whole-archive -I {{PROJECT_DIR}}/libpng-1.6.37/ -o {{FUZZER_NAME}} -lm -lz --libafl-ignore-configurations -fsanitize=undefined --libafl-no-link

[macos]
fuzzer: lib cxx
    cp {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.coverage_ubsan.a {{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.a
    {{LIBAFL_CXX}} {{PROJECT_DIR}}/harness.cc {{PROJECT_DIR}}/target/{{PROFILE_DIR}}/liblibfuzzer_libpng.a -Wl,-force_load,{{PROJECT_DIR}}/libpng-1.6.37/.libs/libpng16.a -I {{PROJECT_DIR}}/libpng-1.6.37/ -o {{FUZZER_NAME}} -lm -lz -framework CoreFoundation -framework Security

[windows]
fuzzer:
    echo "Unsupported on this platform"

[linux]
[macos]
run: fuzzer
    ./{{FUZZER_NAME}} --cores 0-1 --input ./corpus

[windows]
run: fuzzer
    echo "Unsupported on this platform"

[linux]
[macos]
test: fuzzer
    #!/bin/bash
    rm -rf libafl_unix_shmem_server || true
    rm -rf out corpus
    mkdir -p corpus
    printf "\x89PNG\r\n\x1a\n" > corpus/seed.png
    timeout 120s ./{{FUZZER_NAME}} --cores 0-1 --input ./corpus > fuzz_stdout.log 2>&1 || true
    if grep -Eqa "(corpus|objectives): [1-9]" fuzz_stdout.log; then
        echo "Fuzzer is working"
    else
        echo "Fuzzer does not generate any testcases or any crashes"
        exit 1
    fi

[windows]
test: fuzzer
    echo "Unsupported on this platform"

clean:
    rm -rf {{FUZZER_NAME}}
    make -C libpng-1.6.37 clean || true
    cargo clean

