import "../../../just/libafl-qemu.just"
import "../fuzzer_name.just"

ZASAN_SOURCE_DIR := source_directory()

[unix]
compile_zasan:
	#!/bin/bash
	source {{ DOTENV }}
	cargo \
		+nightly \
		build \
		--package zasan \
		--target ${CROSS_TARGET} \
		--profile {{ PROFILE }} \
		--target-dir {{ TARGET_DIR }}

[unix]
fix_zasan:
	#!/bin/bash
	source {{ DOTENV }}
	cargo \
		+nightly \
		fix \
		--package zasan \
		--target ${CROSS_TARGET} \
		--profile {{ PROFILE }} \
		--target-dir {{ TARGET_DIR }} \
		--features test \
		--allow-dirty

[unix]
link_zasan: compile_zasan
	#!/bin/bash
	source {{ DOTENV }}
	${CROSS_CC} \
		${LIBRASAN_CFLAGS} \
		-shared \
		-nodefaultlibs \
		-nostartfiles \
		-nostdlib \
		-g \
		-u aligned_alloc \
		-u atoi \
		-u atol \
		-u atoll \
		-u calloc \
		-u free \
		-u malloc \
		-u malloc_usable_size \
		-u memalign \
		-u memset \
		-u mmap \
		-u munmap \
		-u posix_memalign \
		-u pvalloc \
		-u read \
		-u realloc \
		-u reallocarray \
		-u valloc \
		-u write \
		-o {{ TARGET_DIR }}/$CROSS_TARGET/{{ PROFILE_DIR }}/_libzasan.so \
		-Wl,--version-script={{ ZASAN_SOURCE_DIR }}/zasan.map \
		-Wl,--gc-sections \
		-Wl,--no-undefined \
		{{ TARGET_DIR }}/$CROSS_TARGET/{{ PROFILE_DIR }}/libzasan.a \

[unix]
strip_zasan: link_zasan
	#!/bin/bash
	source {{ DOTENV }}
	${CROSS_STRIP} \
		--strip-unneeded \
		-o {{ TARGET_DIR }}/$CROSS_TARGET/{{ PROFILE_DIR }}/libzasan.so \
		{{ TARGET_DIR }}/$CROSS_TARGET/{{ PROFILE_DIR }}/_libzasan.so

[unix]
build_zasan: strip_zasan
